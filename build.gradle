import java.time.ZonedDateTime
import java.time.temporal.ChronoUnit

plugins {
    id 'org.springframework.boot' version '2.5.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'application'
    id 'idea'
}

group = 'club.w0sv'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'javax.measure', name: 'unit-api', version: '2.1.3'
    implementation group: 'tech.units', name: 'indriya', version: '2.0.4'
    implementation group: 'systems.uom', name: 'systems-common', version: '2.0.2'
    implementation group: 'systems.uom', name: 'systems-quantity', version: '2.0.2'
    implementation group: 'systems.uom', name: 'systems-unicode', version: '2.0.2'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

test {
    useJUnitPlatform()
}

task nonGuiTest(type: Test) {
    useJUnitPlatform {
        excludeTags 'gui'
    }
}

task guiTest(type: Test) {
    useJUnitPlatform {
        includeTags 'gui'
    }
}

bootJar {
    manifest {
        attributes "Implementation-Name": project.name
        attributes "Implementation-Version": project.version
        attributes "Implementation-URL": "https://github.com/bemace/SpotterLevel3"
        attributes "Build-Time": ZonedDateTime.now().truncatedTo(ChronoUnit.SECONDS).format(java.time.format.DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes "Build-Host": InetAddress.getLocalHost().getHostName()
        attributes "Build-OS": System.getProperty("os.name", "unknown") + " v" + System.getProperty("os.version", "?")
        attributes "Build-Tool": "Gradle " + project.gradle.gradleVersion
        // TODO: add some of GitHub's variables: https://docs.github.com/en/actions/reference/environment-variables#default-environment-variables
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

